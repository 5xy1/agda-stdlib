<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Nullary.Negation.Core</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Core properties related to negation</a>
<a id="145" class="Comment">------------------------------------------------------------------------</a>

<a id="219" class="Symbol">{-#</a> <a id="223" class="Keyword">OPTIONS</a> <a id="231" class="Pragma">--without-K</a> <a id="243" class="Pragma">--safe</a> <a id="250" class="Symbol">#-}</a>

<a id="255" class="Keyword">module</a> <a id="262" href="Relation.Nullary.Negation.Core.html" class="Module">Relation.Nullary.Negation.Core</a> <a id="293" class="Keyword">where</a>

<a id="300" class="Keyword">open</a> <a id="305" class="Keyword">import</a> <a id="312" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="327" class="Keyword">using</a> <a id="333" class="Symbol">(</a><a id="334" href="Data.Bool.Base.html#932" class="Function">not</a><a id="337" class="Symbol">)</a>
<a id="339" class="Keyword">open</a> <a id="344" class="Keyword">import</a> <a id="351" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="362" class="Keyword">open</a> <a id="367" class="Keyword">import</a> <a id="374" href="Data.Product.html" class="Module">Data.Product</a>
<a id="387" class="Keyword">open</a> <a id="392" class="Keyword">import</a> <a id="399" href="Function.Base.html" class="Module">Function.Base</a> <a id="413" class="Keyword">using</a> <a id="419" class="Symbol">(</a><a id="420" href="Function.Base.html#1554" class="Function">flip</a><a id="424" class="Symbol">;</a> <a id="426" href="Function.Base.html#1919" class="Function Operator">_$_</a><a id="429" class="Symbol">;</a> <a id="431" href="Function.Base.html#1031" class="Function Operator">_∘_</a><a id="434" class="Symbol">;</a> <a id="436" href="Function.Base.html#636" class="Function">const</a><a id="441" class="Symbol">)</a>
<a id="443" class="Keyword">open</a> <a id="448" class="Keyword">import</a> <a id="455" href="Level.html" class="Module">Level</a>
<a id="461" class="Keyword">open</a> <a id="466" class="Keyword">import</a> <a id="473" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="490" class="Keyword">open</a> <a id="495" class="Keyword">import</a> <a id="502" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="517" class="Keyword">using</a> <a id="523" class="Symbol">(</a><a id="524" href="Relation.Unary.html#1101" class="Function">Pred</a><a id="528" class="Symbol">)</a>

<a id="531" class="Keyword">private</a>
  <a id="541" class="Keyword">variable</a>
    <a id="554" href="Relation.Nullary.Negation.Core.html#554" class="Generalizable">a</a> <a id="556" href="Relation.Nullary.Negation.Core.html#556" class="Generalizable">p</a> <a id="558" href="Relation.Nullary.Negation.Core.html#558" class="Generalizable">q</a> <a id="560" href="Relation.Nullary.Negation.Core.html#560" class="Generalizable">w</a> <a id="562" class="Symbol">:</a> <a id="564" href="Agda.Primitive.html#423" class="Postulate">Level</a>
    <a id="574" href="Relation.Nullary.Negation.Core.html#574" class="Generalizable">A</a> <a id="576" class="Symbol">:</a> <a id="578" class="PrimitiveType">Set</a> <a id="582" href="Relation.Nullary.Negation.Core.html#554" class="Generalizable">a</a>
    <a id="588" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="590" class="Symbol">:</a> <a id="592" class="PrimitiveType">Set</a> <a id="596" href="Relation.Nullary.Negation.Core.html#556" class="Generalizable">p</a>
    <a id="602" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a> <a id="604" class="Symbol">:</a> <a id="606" class="PrimitiveType">Set</a> <a id="610" href="Relation.Nullary.Negation.Core.html#558" class="Generalizable">q</a>
    <a id="616" href="Relation.Nullary.Negation.Core.html#616" class="Generalizable">Whatever</a> <a id="625" class="Symbol">:</a> <a id="627" class="PrimitiveType">Set</a> <a id="631" href="Relation.Nullary.Negation.Core.html#560" class="Generalizable">w</a>

<a id="634" class="Comment">------------------------------------------------------------------------</a>
<a id="707" class="Comment">-- Uses of negation</a>

<a id="contradiction"></a><a id="728" href="Relation.Nullary.Negation.Core.html#728" class="Function">contradiction</a> <a id="742" class="Symbol">:</a> <a id="744" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="746" class="Symbol">→</a> <a id="748" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="750" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="752" class="Symbol">→</a> <a id="754" href="Relation.Nullary.Negation.Core.html#616" class="Generalizable">Whatever</a>
<a id="763" href="Relation.Nullary.Negation.Core.html#728" class="Function">contradiction</a> <a id="777" href="Relation.Nullary.Negation.Core.html#777" class="Bound">p</a> <a id="779" href="Relation.Nullary.Negation.Core.html#779" class="Bound">¬p</a> <a id="782" class="Symbol">=</a> <a id="784" href="Data.Empty.html#628" class="Function">⊥-elim</a> <a id="791" class="Symbol">(</a><a id="792" href="Relation.Nullary.Negation.Core.html#779" class="Bound">¬p</a> <a id="795" href="Relation.Nullary.Negation.Core.html#777" class="Bound">p</a><a id="796" class="Symbol">)</a>

<a id="contraposition"></a><a id="799" href="Relation.Nullary.Negation.Core.html#799" class="Function">contraposition</a> <a id="814" class="Symbol">:</a> <a id="816" class="Symbol">(</a><a id="817" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="819" class="Symbol">→</a> <a id="821" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a><a id="822" class="Symbol">)</a> <a id="824" class="Symbol">→</a> <a id="826" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="828" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a> <a id="830" class="Symbol">→</a> <a id="832" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="834" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a>
<a id="836" href="Relation.Nullary.Negation.Core.html#799" class="Function">contraposition</a> <a id="851" href="Relation.Nullary.Negation.Core.html#851" class="Bound">f</a> <a id="853" href="Relation.Nullary.Negation.Core.html#853" class="Bound">¬q</a> <a id="856" href="Relation.Nullary.Negation.Core.html#856" class="Bound">p</a> <a id="858" class="Symbol">=</a> <a id="860" href="Relation.Nullary.Negation.Core.html#728" class="Function">contradiction</a> <a id="874" class="Symbol">(</a><a id="875" href="Relation.Nullary.Negation.Core.html#851" class="Bound">f</a> <a id="877" href="Relation.Nullary.Negation.Core.html#856" class="Bound">p</a><a id="878" class="Symbol">)</a> <a id="880" href="Relation.Nullary.Negation.Core.html#853" class="Bound">¬q</a>

<a id="884" class="Comment">-- Note also the following use of flip:</a>

<a id="925" class="Keyword">private</a>
  <a id="note"></a><a id="935" href="Relation.Nullary.Negation.Core.html#935" class="Function">note</a> <a id="940" class="Symbol">:</a> <a id="942" class="Symbol">(</a><a id="943" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="945" class="Symbol">→</a> <a id="947" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="949" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a><a id="950" class="Symbol">)</a> <a id="952" class="Symbol">→</a> <a id="954" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a> <a id="956" class="Symbol">→</a> <a id="958" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="960" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a>
  <a id="964" href="Relation.Nullary.Negation.Core.html#935" class="Function">note</a> <a id="969" class="Symbol">=</a> <a id="971" href="Function.Base.html#1554" class="Function">flip</a>

<a id="977" class="Comment">-- If we can decide P, then we can decide its negation.</a>

<a id="¬-reflects"></a><a id="1034" href="Relation.Nullary.Negation.Core.html#1034" class="Function">¬-reflects</a> <a id="1045" class="Symbol">:</a> <a id="1047" class="Symbol">∀</a> <a id="1049" class="Symbol">{</a><a id="1050" href="Relation.Nullary.Negation.Core.html#1050" class="Bound">b</a><a id="1051" class="Symbol">}</a> <a id="1053" class="Symbol">→</a> <a id="1055" href="Relation.Nullary.html#854" class="Datatype">Reflects</a> <a id="1064" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="1066" href="Relation.Nullary.Negation.Core.html#1050" class="Bound">b</a> <a id="1068" class="Symbol">→</a> <a id="1070" href="Relation.Nullary.html#854" class="Datatype">Reflects</a> <a id="1079" class="Symbol">(</a><a id="1080" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1082" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a><a id="1083" class="Symbol">)</a> <a id="1085" class="Symbol">(</a><a id="1086" href="Data.Bool.Base.html#932" class="Function">not</a> <a id="1090" href="Relation.Nullary.Negation.Core.html#1050" class="Bound">b</a><a id="1091" class="Symbol">)</a>
<a id="1093" href="Relation.Nullary.Negation.Core.html#1034" class="Function">¬-reflects</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Relation.Nullary.html#902" class="InductiveConstructor">ofʸ</a>  <a id="1110" href="Relation.Nullary.Negation.Core.html#1110" class="Bound">p</a><a id="1111" class="Symbol">)</a> <a id="1113" class="Symbol">=</a> <a id="1115" href="Relation.Nullary.html#939" class="InductiveConstructor">ofⁿ</a> <a id="1119" class="Symbol">(</a><a id="1120" href="Function.Base.html#1919" class="Function Operator">_$</a> <a id="1123" href="Relation.Nullary.Negation.Core.html#1110" class="Bound">p</a><a id="1124" class="Symbol">)</a>
<a id="1126" href="Relation.Nullary.Negation.Core.html#1034" class="Function">¬-reflects</a> <a id="1137" class="Symbol">(</a><a id="1138" href="Relation.Nullary.html#939" class="InductiveConstructor">ofⁿ</a> <a id="1142" href="Relation.Nullary.Negation.Core.html#1142" class="Bound">¬p</a><a id="1144" class="Symbol">)</a> <a id="1146" class="Symbol">=</a> <a id="1148" href="Relation.Nullary.html#902" class="InductiveConstructor">ofʸ</a> <a id="1152" href="Relation.Nullary.Negation.Core.html#1142" class="Bound">¬p</a>

<a id="¬?"></a><a id="1156" href="Relation.Nullary.Negation.Core.html#1156" class="Function">¬?</a> <a id="1159" class="Symbol">:</a> <a id="1161" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="1165" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="1167" class="Symbol">→</a> <a id="1169" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="1173" class="Symbol">(</a><a id="1174" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1176" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a><a id="1177" class="Symbol">)</a>
<a id="1179" href="Relation.Nullary.html#1581" class="Field">does</a>  <a id="1185" class="Symbol">(</a><a id="1186" href="Relation.Nullary.Negation.Core.html#1156" class="Function">¬?</a> <a id="1189" href="Relation.Nullary.Negation.Core.html#1189" class="Bound">p?</a><a id="1191" class="Symbol">)</a> <a id="1193" class="Symbol">=</a> <a id="1195" href="Data.Bool.Base.html#932" class="Function">not</a> <a id="1199" class="Symbol">(</a><a id="1200" href="Relation.Nullary.html#1581" class="Field">does</a> <a id="1205" href="Relation.Nullary.Negation.Core.html#1189" class="Bound">p?</a><a id="1207" class="Symbol">)</a>
<a id="1209" href="Relation.Nullary.html#1598" class="Field">proof</a> <a id="1215" class="Symbol">(</a><a id="1216" href="Relation.Nullary.Negation.Core.html#1156" class="Function">¬?</a> <a id="1219" href="Relation.Nullary.Negation.Core.html#1219" class="Bound">p?</a><a id="1221" class="Symbol">)</a> <a id="1223" class="Symbol">=</a> <a id="1225" href="Relation.Nullary.Negation.Core.html#1034" class="Function">¬-reflects</a> <a id="1236" class="Symbol">(</a><a id="1237" href="Relation.Nullary.html#1598" class="Field">proof</a> <a id="1243" href="Relation.Nullary.Negation.Core.html#1219" class="Bound">p?</a><a id="1245" class="Symbol">)</a>

<a id="1248" class="Comment">------------------------------------------------------------------------</a>
<a id="1321" class="Comment">-- Quantifier juggling</a>

<a id="1345" class="Keyword">module</a> <a id="1352" href="Relation.Nullary.Negation.Core.html#1352" class="Module">_</a> <a id="1354" class="Symbol">{</a><a id="1355" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1357" class="Symbol">:</a> <a id="1359" href="Relation.Unary.html#1101" class="Function">Pred</a> <a id="1364" href="Relation.Nullary.Negation.Core.html#574" class="Generalizable">A</a> <a id="1366" href="Relation.Nullary.Negation.Core.html#556" class="Generalizable">p</a><a id="1367" class="Symbol">}</a> <a id="1369" class="Keyword">where</a>

  <a id="1378" href="Relation.Nullary.Negation.Core.html#1378" class="Function">∃⟶¬∀¬</a> <a id="1384" class="Symbol">:</a> <a id="1386" href="Data.Product.html#1369" class="Function">∃</a> <a id="1388" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1390" class="Symbol">→</a> <a id="1392" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1394" class="Symbol">(∀</a> <a id="1397" href="Relation.Nullary.Negation.Core.html#1397" class="Bound">x</a> <a id="1399" class="Symbol">→</a> <a id="1401" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1403" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1405" href="Relation.Nullary.Negation.Core.html#1397" class="Bound">x</a><a id="1406" class="Symbol">)</a>
  <a id="1410" href="Relation.Nullary.Negation.Core.html#1378" class="Function">∃⟶¬∀¬</a> <a id="1416" class="Symbol">=</a> <a id="1418" href="Function.Base.html#1554" class="Function">flip</a> <a id="1423" href="Data.Product.html#3422" class="Function">uncurry</a>

  <a id="1434" href="Relation.Nullary.Negation.Core.html#1434" class="Function">∀⟶¬∃¬</a> <a id="1440" class="Symbol">:</a> <a id="1442" class="Symbol">(∀</a> <a id="1445" href="Relation.Nullary.Negation.Core.html#1445" class="Bound">x</a> <a id="1447" class="Symbol">→</a> <a id="1449" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1451" href="Relation.Nullary.Negation.Core.html#1445" class="Bound">x</a><a id="1452" class="Symbol">)</a> <a id="1454" class="Symbol">→</a> <a id="1456" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1458" href="Data.Product.html#1369" class="Function">∃</a> <a id="1460" class="Symbol">λ</a> <a id="1462" href="Relation.Nullary.Negation.Core.html#1462" class="Bound">x</a> <a id="1464" class="Symbol">→</a> <a id="1466" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1468" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1470" href="Relation.Nullary.Negation.Core.html#1462" class="Bound">x</a>
  <a id="1474" href="Relation.Nullary.Negation.Core.html#1434" class="Function">∀⟶¬∃¬</a> <a id="1480" href="Relation.Nullary.Negation.Core.html#1480" class="Bound">∀xPx</a> <a id="1485" class="Symbol">(</a><a id="1486" href="Relation.Nullary.Negation.Core.html#1486" class="Bound">x</a> <a id="1488" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1490" href="Relation.Nullary.Negation.Core.html#1490" class="Bound">¬Px</a><a id="1493" class="Symbol">)</a> <a id="1495" class="Symbol">=</a> <a id="1497" href="Relation.Nullary.Negation.Core.html#1490" class="Bound">¬Px</a> <a id="1501" class="Symbol">(</a><a id="1502" href="Relation.Nullary.Negation.Core.html#1480" class="Bound">∀xPx</a> <a id="1507" href="Relation.Nullary.Negation.Core.html#1486" class="Bound">x</a><a id="1508" class="Symbol">)</a>

  <a id="1513" href="Relation.Nullary.Negation.Core.html#1513" class="Function">¬∃⟶∀¬</a> <a id="1519" class="Symbol">:</a> <a id="1521" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1523" href="Data.Product.html#1369" class="Function">∃</a> <a id="1525" class="Symbol">(λ</a> <a id="1528" href="Relation.Nullary.Negation.Core.html#1528" class="Bound">x</a> <a id="1530" class="Symbol">→</a> <a id="1532" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1534" href="Relation.Nullary.Negation.Core.html#1528" class="Bound">x</a><a id="1535" class="Symbol">)</a> <a id="1537" class="Symbol">→</a> <a id="1539" class="Symbol">∀</a> <a id="1541" href="Relation.Nullary.Negation.Core.html#1541" class="Bound">x</a> <a id="1543" class="Symbol">→</a> <a id="1545" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1547" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1549" href="Relation.Nullary.Negation.Core.html#1541" class="Bound">x</a>
  <a id="1553" href="Relation.Nullary.Negation.Core.html#1513" class="Function">¬∃⟶∀¬</a> <a id="1559" class="Symbol">=</a> <a id="1561" href="Data.Product.html#3265" class="Function">curry</a>

  <a id="1570" href="Relation.Nullary.Negation.Core.html#1570" class="Function">∀¬⟶¬∃</a> <a id="1576" class="Symbol">:</a> <a id="1578" class="Symbol">(∀</a> <a id="1581" href="Relation.Nullary.Negation.Core.html#1581" class="Bound">x</a> <a id="1583" class="Symbol">→</a> <a id="1585" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1587" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1589" href="Relation.Nullary.Negation.Core.html#1581" class="Bound">x</a><a id="1590" class="Symbol">)</a> <a id="1592" class="Symbol">→</a> <a id="1594" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1596" href="Data.Product.html#1369" class="Function">∃</a> <a id="1598" class="Symbol">(λ</a> <a id="1601" href="Relation.Nullary.Negation.Core.html#1601" class="Bound">x</a> <a id="1603" class="Symbol">→</a> <a id="1605" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1607" href="Relation.Nullary.Negation.Core.html#1601" class="Bound">x</a><a id="1608" class="Symbol">)</a>
  <a id="1612" href="Relation.Nullary.Negation.Core.html#1570" class="Function">∀¬⟶¬∃</a> <a id="1618" class="Symbol">=</a> <a id="1620" href="Data.Product.html#3422" class="Function">uncurry</a>

  <a id="1631" href="Relation.Nullary.Negation.Core.html#1631" class="Function">∃¬⟶¬∀</a> <a id="1637" class="Symbol">:</a> <a id="1639" href="Data.Product.html#1369" class="Function">∃</a> <a id="1641" class="Symbol">(λ</a> <a id="1644" href="Relation.Nullary.Negation.Core.html#1644" class="Bound">x</a> <a id="1646" class="Symbol">→</a> <a id="1648" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1650" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1652" href="Relation.Nullary.Negation.Core.html#1644" class="Bound">x</a><a id="1653" class="Symbol">)</a> <a id="1655" class="Symbol">→</a> <a id="1657" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1659" class="Symbol">(∀</a> <a id="1662" href="Relation.Nullary.Negation.Core.html#1662" class="Bound">x</a> <a id="1664" class="Symbol">→</a> <a id="1666" href="Relation.Nullary.Negation.Core.html#1355" class="Bound">P</a> <a id="1668" href="Relation.Nullary.Negation.Core.html#1662" class="Bound">x</a><a id="1669" class="Symbol">)</a>
  <a id="1673" href="Relation.Nullary.Negation.Core.html#1631" class="Function">∃¬⟶¬∀</a> <a id="1679" class="Symbol">=</a> <a id="1681" href="Function.Base.html#1554" class="Function">flip</a> <a id="1686" href="Relation.Nullary.Negation.Core.html#1434" class="Function">∀⟶¬∃¬</a>

<a id="1693" class="Comment">------------------------------------------------------------------------</a>
<a id="1766" class="Comment">-- Double-negation</a>

<a id="¬¬-map"></a><a id="1786" href="Relation.Nullary.Negation.Core.html#1786" class="Function">¬¬-map</a> <a id="1793" class="Symbol">:</a> <a id="1795" class="Symbol">(</a><a id="1796" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="1798" class="Symbol">→</a> <a id="1800" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a><a id="1801" class="Symbol">)</a> <a id="1803" class="Symbol">→</a> <a id="1805" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1807" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1809" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1815" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1817" href="Relation.Nullary.Negation.Core.html#602" class="Generalizable">Q</a>
<a id="1819" href="Relation.Nullary.Negation.Core.html#1786" class="Function">¬¬-map</a> <a id="1826" href="Relation.Nullary.Negation.Core.html#1826" class="Bound">f</a> <a id="1828" class="Symbol">=</a> <a id="1830" href="Relation.Nullary.Negation.Core.html#799" class="Function">contraposition</a> <a id="1845" class="Symbol">(</a><a id="1846" href="Relation.Nullary.Negation.Core.html#799" class="Function">contraposition</a> <a id="1861" href="Relation.Nullary.Negation.Core.html#1826" class="Bound">f</a><a id="1862" class="Symbol">)</a>

<a id="1865" class="Comment">-- Stability under double-negation.</a>

<a id="Stable"></a><a id="1902" href="Relation.Nullary.Negation.Core.html#1902" class="Function">Stable</a> <a id="1909" class="Symbol">:</a> <a id="1911" class="PrimitiveType">Set</a> <a id="1915" href="Relation.Nullary.Negation.Core.html#556" class="Generalizable">p</a> <a id="1917" class="Symbol">→</a> <a id="1919" class="PrimitiveType">Set</a> <a id="1923" href="Relation.Nullary.Negation.Core.html#556" class="Generalizable">p</a>
<a id="1925" href="Relation.Nullary.Negation.Core.html#1902" class="Function">Stable</a> <a id="1932" href="Relation.Nullary.Negation.Core.html#1932" class="Bound">P</a> <a id="1934" class="Symbol">=</a> <a id="1936" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1938" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="1940" href="Relation.Nullary.Negation.Core.html#1932" class="Bound">P</a> <a id="1942" class="Symbol">→</a> <a id="1944" href="Relation.Nullary.Negation.Core.html#1932" class="Bound">P</a>

<a id="1947" class="Comment">-- Everything is stable in the double-negation monad.</a>

<a id="stable"></a><a id="2002" href="Relation.Nullary.Negation.Core.html#2002" class="Function">stable</a> <a id="2009" class="Symbol">:</a> <a id="2011" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="2013" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="2015" href="Relation.Nullary.Negation.Core.html#1902" class="Function">Stable</a> <a id="2022" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a>
<a id="2024" href="Relation.Nullary.Negation.Core.html#2002" class="Function">stable</a> <a id="2031" href="Relation.Nullary.Negation.Core.html#2031" class="Bound">¬[¬¬p→p]</a> <a id="2040" class="Symbol">=</a> <a id="2042" href="Relation.Nullary.Negation.Core.html#2031" class="Bound">¬[¬¬p→p]</a> <a id="2051" class="Symbol">(λ</a> <a id="2054" href="Relation.Nullary.Negation.Core.html#2054" class="Bound">¬¬p</a> <a id="2058" class="Symbol">→</a> <a id="2060" href="Data.Empty.html#628" class="Function">⊥-elim</a> <a id="2067" class="Symbol">(</a><a id="2068" href="Relation.Nullary.Negation.Core.html#2054" class="Bound">¬¬p</a> <a id="2072" class="Symbol">(</a><a id="2073" href="Relation.Nullary.Negation.Core.html#2031" class="Bound">¬[¬¬p→p]</a> <a id="2082" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="2084" href="Function.Base.html#636" class="Function">const</a><a id="2089" class="Symbol">)))</a>

<a id="2094" class="Comment">-- Negated predicates are stable.</a>

<a id="negated-stable"></a><a id="2129" href="Relation.Nullary.Negation.Core.html#2129" class="Function">negated-stable</a> <a id="2144" class="Symbol">:</a> <a id="2146" href="Relation.Nullary.Negation.Core.html#1902" class="Function">Stable</a> <a id="2153" class="Symbol">(</a><a id="2154" href="Relation.Nullary.html#656" class="Function Operator">¬</a> <a id="2156" href="Relation.Nullary.Negation.Core.html#588" class="Generalizable">P</a><a id="2157" class="Symbol">)</a>
<a id="2159" href="Relation.Nullary.Negation.Core.html#2129" class="Function">negated-stable</a> <a id="2174" href="Relation.Nullary.Negation.Core.html#2174" class="Bound">¬¬¬P</a> <a id="2179" href="Relation.Nullary.Negation.Core.html#2179" class="Bound">P</a> <a id="2181" class="Symbol">=</a> <a id="2183" href="Relation.Nullary.Negation.Core.html#2174" class="Bound">¬¬¬P</a> <a id="2188" class="Symbol">(λ</a> <a id="2191" href="Relation.Nullary.Negation.Core.html#2191" class="Bound">¬P</a> <a id="2194" class="Symbol">→</a> <a id="2196" href="Relation.Nullary.Negation.Core.html#2191" class="Bound">¬P</a> <a id="2199" href="Relation.Nullary.Negation.Core.html#2179" class="Bound">P</a><a id="2200" class="Symbol">)</a>
</pre></body></html>